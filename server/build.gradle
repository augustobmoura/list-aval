import groovy.json.JsonOutput
import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

plugins {
	id 'org.springframework.boot' version '2.1.8.RELEASE'
	id 'io.spring.dependency-management' version '1.0.8.RELEASE'
	id 'java'
	id 'groovy'
	id 'org.openapi.generator' version '4.2.1'
}

group = 'br.ufg.inf.es'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '8'

repositories {
	mavenLocal()
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'com.vladmihalcea:hibernate-types-52:2.7.1'
	implementation 'org.springdoc:springdoc-openapi-ui:1.2.0'
	implementation 'com.fasterxml.jackson.datatype:jackson-datatype-hibernate5'
//	implementation 'br.ufg.inf.es:listaplic-client:v1'
	implementation project(':generated:listaplic-client')
	implementation project(':generated:listelab-client')

	runtimeOnly 'org.postgresql:postgresql'

	compileOnly 'org.projectlombok:lombok:1.18.10'
	annotationProcessor 'org.projectlombok:lombok:1.18.10'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
	testImplementation 'org.spockframework:spock-spring:1.3-groovy-2.5'
	testImplementation 'com.h2database:h2:1.4.199'
}

jar {
	baseName = 'listaval'
	version = project.version
}

String listAplicSpec = "$rootDir/spec/list-aplic.json"
String listElabSpec = "$rootDir/spec/list-elab.json"

task downloadListAplicSpec(group: 'download') {
	outputs.file(listAplicSpec)

	doLast {
		URL specUrl = new URL('https://list-aplic-api.herokuapp.com/api/v2/api-docs')
		File out = file(listAplicSpec)

		out.text = JsonOutput.prettyPrint(specUrl.text)
	}
}

task downloadElabAplicSpec(group: 'download') {
	outputs.file(listElabSpec)

	doLast {
		URL specUrl = new URL('http://api.sifo.tech/swagger/v1/swagger.json')
		File out = file(listElabSpec)

		out.text = JsonOutput.prettyPrint(specUrl.text)
	}
}

task generateListAplicClient(type: GenerateTask, group: 'build') {
	generatorName = 'java'
	inputSpec = listAplicSpec
	outputDir = 'generated/listaplic-client'
	apiPackage = 'br.ufg.inf.es.listaplic'
	invokerPackage = 'br.ufg.inf.es.listaplic.invoker'
	modelPackage = 'br.ufg.inf.es.listaplic.model'
	version = '1.0.0-SNAPSHOT'
	additionalProperties = [
			groupId                : 'br.ufg.inf.es',
			artifactId             : 'listaplic-client',
			hideGenerationTimestamp: 'true',
	]
	configOptions = [
			dateLibrary: 'java8'
	]
}

task generateListElabClient(type: GenerateTask, group: 'build') {
	String out = "$rootDir/generated/listelab-client"

	inputs.file(listElabSpec)
	outputs.dir(out)

	generatorName = 'java'
	inputSpec = listElabSpec
	outputDir = out
	apiPackage = 'br.ufg.inf.es.listelab'
	invokerPackage = 'br.ufg.inf.es.listelab.invoker'
	modelPackage = 'br.ufg.inf.es.listelab.model'
	version = '1.0.0-SNAPSHOT'
	skipValidateSpec = true
	additionalProperties = [
			groupId   : 'br.ufg.inf.es',
			artifactId: 'listelab-client',
			hideGenerationTimestamp: 'true',
	]
	configOptions = [
			dateLibrary: 'java8'
	]
}

compileJava.dependsOn generateListElabClient
compileJava.dependsOn generateListAplicClient
