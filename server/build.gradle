plugins {
	id 'org.springframework.boot' version '2.1.8.RELEASE'
	id 'io.spring.dependency-management' version '1.0.8.RELEASE'
	id 'java'
	id 'groovy'
	id 'org.openapi.generator' version '4.2.1'
}

group = 'br.ufg.inf.es'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '8'

repositories {
	mavenLocal()
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'com.vladmihalcea:hibernate-types-52:2.7.1'
	implementation 'org.springdoc:springdoc-openapi-ui:1.2.0'
	implementation 'com.fasterxml.jackson.datatype:jackson-datatype-hibernate5'
//	implementation 'br.ufg.inf.es:listaplic-client:v1'
	implementation project(':generated:listaplic-client')
	implementation project(':generated:listelab-client')

	runtimeOnly 'org.postgresql:postgresql'

	compileOnly 'org.projectlombok:lombok:1.18.10'
	annotationProcessor 'org.projectlombok:lombok:1.18.10'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
	testImplementation 'org.spockframework:spock-spring:1.3-groovy-2.5'
	testImplementation 'com.h2database:h2:1.4.199'
}

jar {
	baseName = 'listaval'
	version = project.version
}

task generateListAplicClient(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask, group: 'build') {
	generatorName = 'java'
	inputSpec = 'https://list-aplic-api.herokuapp.com/api/v2/api-docs'
	outputDir = 'generated/listaplic-client'
	apiPackage = 'br.ufg.inf.es.listaplic'
	invokerPackage = 'br.ufg.inf.es.listaplic.invoker'
	modelPackage = 'br.ufg.inf.es.listaplic.model'
	version = '1.0.0-SNAPSHOT'
	additionalProperties = [
			groupId   : 'br.ufg.inf.es',
			artifactId: 'listaplic-client'
	]
	configOptions = [
			dateLibrary: 'java8'
	]
}

task generateListElabClient(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask, group: 'build') {
	generatorName = 'java'
	inputSpec = 'spec/list-elab-spec.json'
	outputDir = 'generated/listelab-client'
	apiPackage = 'br.ufg.inf.es.listelab'
	invokerPackage = 'br.ufg.inf.es.listelab.invoker'
	modelPackage = 'br.ufg.inf.es.listelab.model'
	version = '1.0.0-SNAPSHOT'
	skipValidateSpec = true
	additionalProperties = [
			groupId   : 'br.ufg.inf.es',
			artifactId: 'listelab-client'
	]
	configOptions = [
			dateLibrary: 'java8'
	]
}

compileJava.dependsOn generateListElabClient
compileJava.dependsOn generateListAplicClient
